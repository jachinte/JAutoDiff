plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0' apply false
}

subprojects { project ->
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'com.rigiresearch'
    version = '0.0.1-SNAPSHOT'

    java {
        withSourcesJar()
    }

    build.finalizedBy(shadowJar)

    repositories {
        jcenter()
        flatDir {
            dirs "$rootDir/libs"
        }
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
    }
}
